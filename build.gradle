apply plugin: 'groovy'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '4.4.0.201606070830-r'
}

task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.dmdirc'
            artifactId 'git-version'
            version '1.1'

            from components.java

            artifact sourceJar {
                classifier 'sources'
            }

            pom.withXml {
                def root = asNode()
                root.appendNode('inceptionYear', '2014')
                root.appendNode('url', 'https://github.com/DMDirc/gradle-git-version-plugin')
                root.appendNode('name', "${groupId}:${artifactId}")
                root.appendNode('description', 'Plugin to automatically apply versions based on git tags')

                def scm = root.appendNode('scm')
                scm.appendNode('url', 'https://github.com/DMDirc/gradle-git-version-plugin')
                scm.appendNode('connection', 'scm:https://github.com/DMDirc/gradle-git-version-plugin.git')
                scm.appendNode('developerConnection', 'scm:ssh://git@github.com:DMDirc/gradle-git-version-plugin.git')

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'MIT License')
                license.appendNode('url', 'http://www.opensource.org/licenses/mit-license.php')
                license.appendNode('distribution', 'repo')
            }
        }
    }
    repositories.mavenLocal()
}

task wrapper(type: Wrapper) {
    description 'Downloads and installs an up-to-date wrapper for gradle.'
    gradleVersion = '2.14'
}
